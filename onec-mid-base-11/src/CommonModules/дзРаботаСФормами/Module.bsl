#Область ПрограммныйИнтерфейс

// Дополнить форму.
// Добавляет реквизиты на форму документа
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ДополнитьФорму(Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		
		ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма);
		ДобавитьЭлементыДляРасчетаПоСогласованнойСкидке(Форма);
	
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда	
		
		ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма);

	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда	
		
		ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма);

	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда	
		
		ВставитьКонтактноеЛицоПередРеквизитомСумма(Форма);

	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		
		ВставитьКонтактноеЛицоПередРеквизитомОснование(Форма);
		
	КонецЕсли;	

КонецПроцедуры
	
#КонецОбласти


#Область СлужебныепроцедурыИФункции

Процедура ДобавитьКонтактноеЛицоВГруппуШапкаПраво(Форма)
	
	ЭлементКонтактноеЛицо = Форма.Элементы.Добавить("дзКонтактноеЛицо",Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.дзКонтактноеЛицо";	
	
КонецПроцедуры 

Процедура ВставитьКонтактноеЛицоПередРеквизитомСумма(Форма)

	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("дзКонтактноеЛицо",Тип("ПолеФормы"),,Форма.Элементы.СуммаДокумента);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.дзКонтактноеЛицо";	

КонецПроцедуры 

Процедура ВставитьКонтактноеЛицоПередРеквизитомОснование(Форма)

	ЭлементКонтактноеЛицо = Форма.Элементы.Вставить("дзКонтактноеЛицо",Тип("ПолеФормы"),,Форма.Элементы.Основание);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.дзКонтактноеЛицо";	

КонецПроцедуры


Процедура ДобавитьЭлементыДляРасчетаПоСогласованнойСкидке(Форма)

	Группа = Форма.Элементы.Вставить("ГруппаСкидка",Тип("ГруппаФормы"),,Форма.Элементы.Страницы);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	
	ПолеСкидка = Форма.Элементы.Добавить("дзСогласованнаяСкидка",Тип("ПолеФормы"),Группа);
	ПолеСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСкидка.ПутьКДанным = "Объект.дзСогласованнаяСкидка";
	ПолеСкидка.УстановитьДействие("ПриИзменении", "дзСогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитатьТаблицу = Форма.Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчитатьТаблицу.Заголовок = "Пересчитать таблицу";
	КомандаПересчитатьТаблицу.Действие = "ПересчитатьТаблицу";
	КомандаПересчитатьТаблицу.Картинка = БиблиотекаКартинок.Обновить;
	КомандаПересчитатьТаблицу.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	КнопкаПересчитатьТаблицу = Форма.Элементы.Добавить("ПересчитатьТаблицу",Тип("КнопкаФормы"),Группа);
	КнопкаПересчитатьТаблицу.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаПересчитатьТаблицу.ИмяКоманды = "ПересчитатьТаблицу"; 

КонецПроцедуры

#КонецОбласти
